'=======================================================================================
'Unifloc 7.9  Vulpes zerda                                           khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
' функции расчета скважины для проведения расчетов из интерфейса Excel
Option Explicit
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета линейного давления по забойному для скважины
' расчет снизу-вверх, простой и быстрый расчет
Public Function well_plin_pwf_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal pwf_atma As Double, _
        Optional ByVal Pcas_atma As Double = -1, _
        Optional ByVal str_well As String = WELL_DEFAULT, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal str_ESP As String = 0, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal ksep_fr As Double = 0, _
        Optional ByVal c_calibr_head_d As Double = 0, _
        Optional ByVal c_calibr_power_d As Double = 0, _
        Optional ByVal c_calibr_rate_d As Double = 0, _
        Optional ByVal param_num As Integer = 1 _
                                    )
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' pwf_atma - забойное давление
'
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d - коэффициент деградации напора УЭЦН
' c_calibr_power_d - коэффициент деградации мощности УЭЦН
' c_calibr_rate_d - коэффициент деградации расхода УЭЦН
' param_num - параметры для вывода в качестве результата
'           на нулевой позиции выходного массива,
'           полный набор результатов выводится в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, Pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d, c_calibr_power_d, c_calibr_rate_d) 'VFM
    ' call calc method
    Call well.calc_plin_pwf_atma(pwf_atma)
    ' return results
    well_plin_pwf_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета давления на приеме по забойному для скважины
' расчет снизу-вверх, считает только участок ниже насоса
Public Function well_pintake_pwf_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal pwf_atma As Double, _
        Optional ByVal str_well As String = WELL_DEFAULT, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal hydr_corr As H_CORRELATION = 0 _
                                    )
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' pwf_atma - забойное давление
'
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, , _
                             str_well, str_PVT, "0", hydr_corr)
    ' return results
    well_pintake_pwf_atma = well.calc_Pintake_pwf_atma(pwf_atma)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета забойного давления по устьевому для скважины
' расчет сверху-вниз, считает быстро за счет угадывания сепарации
'                     температура только линейная или по градиенту
Public Function well_pwf_plin_atma( _
                 ByVal q_m3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal plin_atma As Double, _
        Optional ByVal Pcas_atma As Double = -1, _
        Optional ByVal str_well As String = WELL_DEFAULT, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal str_ESP As String = 0, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal ksep_fr As Double = -1, _
        Optional ByVal Psep_atma As Double = 40, _
        Optional ByVal Tsep_C As Double = 40, _
        Optional ByVal c_calibr_head_d As Double = 0, _
        Optional ByVal param_num As Integer = 5)
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' plin_atma - линейное (устьевое) давление
'
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция:
'                   BeggsBrill = 0;
'                   Ansari = 1;
'                   Unified = 2;
'                   Gray = 3;
'                   HagedornBrown = 4;
'                   SakharovMokhov = 5.
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' Psep_atma - давление сепарации
' Tsep_C - температура сепарации
'           при расчете сверху вниз неизвестны параметры сепарации
'           если задать их явно (угадать)
'           тогда расчет упрощается и ускоряется
' c_calibr_head_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    Dim well As CWellESP
    Dim ar
    
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, Pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    ' при расчете сверху вниз задаем какие то значения сепарации наугад
    If well.IsESP Then
        Call well.SetKseptotal_d(fullyManual, ksep_fr, 0, Psep_atma, Tsep_C)
        well.isCalcCasing = True
    End If
    
    ' проведем расчет
    Call well.calc_pwf_plin_atma(plin_atma, well.tbh_C, fast:=True)
    ' в качестве результата выведем ряд расчитанных значений давления
    well_pwf_plin_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция адаптации модели скважины по данным эксплуатации
' подбирает коэффициента деградации УЭЦН и штуцера
' по замера на поверхности и на забое/приеме насоса
Public Function well_calcc_calibr_head_fr( _
                     ByVal q_m3day As Double, _
                     ByVal fw_perc As Double, _
                     ByVal Pdown_atma As Double, _
                     ByVal pbuf_atma As Double, _
            Optional Pdown_at_intake As Boolean = False, _
            Optional ByVal plin_atma As Double = -1, _
            Optional ByVal Pcas_atma As Double = -1, _
            Optional ByVal str_well As String = WELL_DEFAULT, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal str_ESP As String = 0, _
            Optional ByVal hydr_corr As H_CORRELATION = 0, _
            Optional ByVal ksep_fr As Double = -1, _
            Optional ByVal c_calibr_head_d As Double = 0, _
            Optional ByVal param_num As Integer = 0)
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
' Pdown_atma - давление ниже насоса (внизу) для расчета
'     либо забойное давление (по умолчанию)
'     либо давление на приеме
'     определяется опциональным параметром Pdown_at_intake
' pbuf_atma  - буферное давление
'
' Pdown_at_intake - флаг определяет точку расчета давления
'                   ниже насоса. По умолчанию забойное
' plin_atma - линейное давление
'             если не задано штуцер не учитывается
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    Dim pwf_temp_atma As Double
    Dim ar
On Error GoTo err1:
    ' read all data to well object
    Set well = well_init_data(q_m3day, fw_perc, Pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    ' calc BHP if needed from intake pressure
    If Pdown_at_intake Then
        pwf_temp_atma = well.calc_pwf_Pintake_atma(Pdown_atma)
    Else
        pwf_temp_atma = Pdown_atma
    End If
    ' проведем расчет
    If well.calc_well(plin_atma, pbuf_atma, pwf_temp_atma, Pcas_atma) Then
    ' в качестве результата выведем ряд расчитанных значений давления
        well_calcc_calibr_head_fr = well_out_arr(well, param_num)
    Else
        well_calcc_calibr_head_fr = "c_calibr_head not found. Check log for details"
    End If
    Exit Function
err1:
    Dim errmsg As String
    errmsg = "well_calcc_calibr_head_fr: error" & sDELIM & Err.Description
    addLogMsg errmsg
    well_calcc_calibr_head_fr = errmsg
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета забойного давления скважины по динамическому уровню
Public Function well_pwf_Hdyn_atma( _
                     ByVal q_m3day As Double, _
                     ByVal fw_perc As Double, _
                     ByVal Pcas_atma As Double, _
                     ByVal Hdyn_m As Double, _
            Optional ByVal str_well As String = WELL_DEFAULT, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal str_ESP As String = 0, _
            Optional ByVal hydr_corr As H_CORRELATION = 0, _
            Optional ByVal ksep_fr As Double = 0, _
            Optional ByVal c_calibr_head_d As Double = 0, _
            Optional ByVal param_num As Integer = 0)
' исходные параметры
' q_m3day   - дебит жидкости, на поверхности
' fw_perc   - обводненность (объемная на поверхности)
' Pcas_atma - затрубное давление
' Hdyn_m    - динамический уровень (при данном затрубном)
'
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr   - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d   - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    
    Dim well As CWellESP
    Set well = well_init_data(q_m3day, fw_perc, Pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    Call well.calc_pwf_PanHd_atma(Pcas_atma, Hdyn_m, well.tbh_C)          ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    Dim ar
    well_pwf_Hdyn_atma = well_out_arr(well, param_num)
    
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета узлового анализа системы пласт - скважина - УЭЦН
' по заданным параетрам пласта, скважины и УЭЦН
' определяется рабочий дебит и забойное давление
Public Function nodal_qliq_sm3day( _
             ByVal pi_sm3dayatm As Double, _
             ByVal plin_atma As Double, _
             ByVal fw_perc As Double, _
    Optional ByVal Pres_atma = 250, _
    Optional ByVal Pcas_atma As Double = 10, _
    Optional ByVal str_well As String = WELL_DEFAULT, _
    Optional ByVal str_PVT As String = PVT_DEFAULT, _
    Optional ByVal str_ESP As String = 0, _
    Optional ByVal hydr_corr As H_CORRELATION = 0, _
    Optional ByVal ksep_fr As Double = 0, _
    Optional ByVal c_calibr_head_d As Double = 0, _
    Optional ByVal param_num As Integer = 23)
' исходные параметры
' pi_sm3dayatm - коэффициент продуктивности пласта
' plin_atma - линейное давление
' fw_perc   - обводненность (объемная на поверхности)
'
' Pres_atma - пластовое давление
' Pcas_atma - затрубное давление (для определения Ндин)
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr  - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr   - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d   - коэффициент деградации УЭЦН
' param_num - параметры для вывода в качестве результата
'             если не задан выводятся все в виде массива
' ----------- результаты расчета
' массив параметры работы системы пласт - скважина - УЭЦН
'description_end
    Dim PS As New CProdSys
    Dim well As CWellESP
    Dim res As New CReservoirVogel
    Dim ar
    ' init all data
    Set well = well_init_data(0, fw_perc, Pcas_atma, _
                             str_well, str_PVT, str_ESP, hydr_corr, ksep_fr, c_calibr_head_d)
    res.InitProp Pres_atma, well.fluid.pb_atma, fw_perc
    res.pi_sm3dayatm = pi_sm3dayatm
    
    Set PS.well = well
    Set PS.Reservoir = res
    
    Call PS.nodal_qliq_sm3day(plin_atma)
    
    well.qliq_sm3day = PS.qsol_scm3day
    
    Call well.calc_plin_pwf_atma(PS.pwfsol_atma)          ' проведем расчет
    ' в качестве результата выведем ряд расчитанных значений давления
    
    nodal_qliq_sm3day = well_out_arr(well, param_num)
    
End Function
' ==============  функции для расчета скважины ==========================
' =====================================================================
' функция для шаблонного чтения данных по скважине в интерфейсных функциях
Private Function well_init_data(q_m3day, _
                               fw_perc, _
                      Optional Pcas_atma As Double = 10, _
                      Optional str_well As String = WELL_DEFAULT, _
                      Optional str_PVT As String = PVT_DEFAULT, _
                      Optional str_ESP As String = 0, _
                      Optional hydr_corr As H_CORRELATION = 0, _
                      Optional ksep_fr As Double = 0, _
                      Optional c_calibr_head_d As Double = 1, _
                      Optional c_calibr_power_d As Double = 1, _
                      Optional c_calibr_rate_d As Double = 1) As CWellESP    'VFM
' исходные параметры
' q_m3day    - дебит жидкости, на поверхности
' fw_perc    - обводненность (объемная на поверхности)
'
' Pcas_atma - затрубное давление
'     если не задано динамический уровень не рассчитывается
' str_well  - закодированные параметры конструкции скважины.
'           Если не указано,
'           используются свойства скважины по умолчанию.
' str_PVT - закодированные параметры флюидов. Если не указано,
'          используются свойства флюида по умолчанию.
' str_ESP - закодированные параметры УЭЦН. Если
'          не задано или задано значение 0
'          то УЭЦН не учитывается, проводится расчет для
'          фонтанирующей скважины.
' hydr_corr      - гидравлическая корреляция, H_CORRELATION
'                   BeggsBrill = 0
'                   Ansari = 1
'                   Unified = 2
'                   Gray = 3
'                   HagedornBrown = 4
'                   SakharovMokhov = 5
' ksep_fr - коэффициент сепарации.
'           если задан - то используется вместо расчетного
'           явное задание коэффициента серации ускоряет расчет
' c_calibr_head_d - коэффициент деградации напора УЭЦН
' c_calibr_power_d - коэффициент деградации мощности УЭЦН
' c_calibr_rate_d - коэффициент деградации расхода УЭЦН
' на входе данные по конструкции скважины, PVT, ЭЦН
' берутся из строк с закодированными параметрами
' следует использовать функции Encode для передачи параметров
' на выходе объект скважина с загруженными данными
    
    Dim well As New CWellESP
    Dim PVT As New CPVT
    Dim pmp As CESPpump
    Dim ESPsystem As New CESPsystemSimple
    ' read PVT data from encoded string
    Set PVT = PVT_decode_string(str_PVT)
    ' set liquid rate
    PVT.qliq_sm3day = q_m3day
    ' set water cut - correcting percent to fraction
    PVT.fw_fr = fw_perc / 100
    ' set well construction from encoded string
    ' also set temperature parameters (BHT and WHT)
    Set well = well_decode_string(str_well)
    ' link well with fluid object
    Set well.fluid = PVT
    ' set casing pressure
    well.Pcas_atma = Pcas_atma
    ' set hydraulic correlation
    well.HFlowCorrelation = hydr_corr
    ' set temperature model with temperature gradient
    well.TempCorrelation = 2    'VFM GeoGradTemp
    ' read ESP object from encoded string
    If Len(str_ESP) < 3 Then
        Set well.ESP = Nothing
    Else
        Set ESPsystem = ESP_decode_string(str_ESP)
        ' link well with ESP object
        Set well.ESP = ESPsystem
        ' set correction factor for ESP
        well.ESP.c_calibr_head = c_calibr_head_d    'VFM
        well.ESP.c_calibr_power = c_calibr_power_d  'VFM
        well.ESP.c_calibr_rate = c_calibr_rate_d    'VFM
        ' set separation coefficient if given
        If ksep_fr > 0 And ksep_fr <= 1 Then
            Call well.SetKseptotal_d(valueManual, ksep_fr, 0)
        Else
            Call well.SetKseptotal_d(byCorrealation, ksep_fr)
        End If
    End If
    
    
    ' return result well object
    Set well_init_data = well
End Function
' function prepares output array for public functions
' подготовим данные для вывода
Private Function well_out_arr(well As CWellESP, Optional FirsrCol As Integer = 0)
' данные выводятся в одну линию, чтобы, хотя бы теоретически, можно было вывести данные по скважине в таблице
' во второй строке выводятся подписи параметров, если необходимо
' первый параметр настраиваемый
    Dim ar1, ar2
    Dim res As New Dictionary
    Const arr_len = 50  'VFM
On Error GoTo err1:
    ReDim ar1(arr_len)
    ReDim ar2(arr_len)
    Dim i As Integer
    With well
         res.Add "Pline_atma", .pline_atma                                  'i=1
         res.Add "pbuf_atma", .pbuf_atma                                    'i=2
         res.Add "Pdis_atma", .Pdis_atma                                    'i=3
         res.Add "Pint_atma", .Pint_atma                                    'i=4
         res.Add "pwf_atma", .pwf_atma                                      'i=5
         res.Add "qliq_sm3day", .qliq_sm3day                                'i=23
         res.Add "fw_perc", .fw_perc                                        'i=24
         res.Add "tbh_C", .tbh_C                                            'i=20
         res.Add "twh_C", .twh_C                                            'i=17
         res.Add "Tsurf_C", .Tsurf_C                                        'i=16
         res.Add "t_dis_C", .t_dis_C                                          'i=18
         res.Add "Tintake_C", .tintake_C                                    'i=19
         res.Add "choke.c_degrad_fr", .choke.c_degrad_fr                      'i=22
         res.Add "Pcas_atma", .Pcas_atma                                    'i=6
         res.Add "Hdyn_m", .Hdyn_m                                          'i=7
         If .IsESP Then
            res.Add "ESP.power_CS_calc_W", .ESP.power_CS_calc_W                'i=8
            res.Add "ESP.freq_Hz", .ESP.freq_Hz                                'i=9
            res.Add "ESP.I_A", .ESP.I_A                                        'i=10
            res.Add "ESP.U_V", .ESP.U_V  'VFM
            res.Add "ESP.load_fr", .ESP.load_fr                                'i=11
            res.Add "ESP.ESPpump.EffiencyESP_d", .ESP.ESPpump.EffiencyESP_d    'i=12
            res.Add "ESP.KsepTotal_fr", .ESP.KsepTotal_fr                      'i=13
            res.Add "ESP.KsepNat_fr", .ESP.KsepNat_fr                          'i=14
            res.Add "ESP.KSepGasSep_fr", .ESP.KSepGasSep_fr                    'i=15
            res.Add "ESP.c_calibr_head", .ESP.c_calibr_head                            'i=21
            res.Add "ESP.c_calibr_power", .ESP.c_calibr_power  'VFM
            res.Add "ESP.c_calibr_rate", .ESP.c_calibr_rate  'VFM
            
            
            'res.Add "ESP.power_motor_fact_W", .ESP.power_motor_fact_W  'VFM empty
            res.Add "ESP.power_motor_nom_W", .ESP.power_motor_nom_W  'VFM
            res.Add "ESP.cable_dU_V", .ESP.cable_dU_V  'VFM
             
            
            res.Add "ESP.dPower_GasSep_W", .ESP.dPower_GasSep_W  'VFM
            res.Add "ESP.dPower_protector_W", .ESP.dPower_protector_W  'VFM
            res.Add "ESP.cable_dPower_W", .ESP.cable_dPower_W  'VFM
            res.Add "ESP.dPower_transform_W", .ESP.dPower_transform_W  'VFM
            res.Add "ESP.dPower_CS_W", .ESP.dPower_CS_W  'VFM
            
            
            res.Add "ESP.ESPpump.Powerfluid_Wt", .ESP.ESPpump.Powerfluid_Wt  'VFM
            res.Add "ESP.ESPpump.PowerESP_Wt", .ESP.ESPpump.PowerESP_Wt  'VFM
            res.Add "ESP.power_shaft_W", .ESP.power_shaft_W  'VFM
            res.Add "ESP.power_motor_W", .ESP.power_motor_W  'VFM
            res.Add "ESP.cable_power_W", .ESP.cable_power_W  'VFM
            res.Add "ESP.power_CS_teor_calc_W", .ESP.power_CS_teor_calc_W  'VFM
            
            
            res.Add "ESP.QmixBeforePump_m3day", .ESP.QmixBeforePump_m3day  'VFM empty
            res.Add "ESP.GasFraction_BeforePump_d", .ESP.GasFraction_BeforePump_d  'VFM empty
            res.Add "ESP.GasFraction_InGasSep_d", .ESP.GasFraction_InGasSep_d  'VFM
            res.Add "ESP.GasFraction_PumpIntake_d", .ESP.GasFraction_PumpIntake_d  'VFM
            res.Add "ESP.GasFraction_PumpOutlet_d", .ESP.GasFraction_PumpOutlet_d  'VFM
            
        End If
        For i = 1 To res.Count
            ar1(i) = res.Items()(i - 1): ar2(i) = res.Keys()(i - 1)
        Next i
        ' first column can have any number
        ' set it here
        ar1(0) = ar1(FirsrCol): ar2(0) = ar2(FirsrCol)
                
        well_out_arr = Array(ar1, ar2)
    ' можно еще добавить сюда вывод кривых распределения давления и температуры по стволу и еще 4 параметров  (потом)
    End With
    Exit Function
err1:
    Resume Next
End Function
