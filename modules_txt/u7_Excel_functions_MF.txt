'=======================================================================================
'Unifloc 7.17  Test                                     khabibullin.ra@gubkin.ru
'Petroleum engineering calculations modules (macroses)
'2000 - 2019
'
'=======================================================================================
'функции для проведения расчетов из интерфейса Excel
'многофазный поток в трубах и элементах инфраструктуры
Option Explicit
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета всех свойств многофазного потока
Public Function MF_all_mf( _
            ByVal qliq_sm3day As Double, _
            ByVal fw_perc As Double, _
            ByVal p_atma As Double, _
            ByVal t_C As Double, _
            Optional ByVal gamma_gas As Double = const_gg_, _
            Optional ByVal gamma_oil As Double = const_go_, _
            Optional ByVal gamma_wat As Double = const_gw_, _
            Optional ByVal rsb_m3m3 = const_rsb_default, _
            Optional ByVal rp_m3m3 = -1, _
            Optional ByVal pb_atma = -1, _
            Optional ByVal tres_C = const_tres_default, _
            Optional ByVal bob_m3m3 = -1, _
            Optional ByVal muob_cP = -1, _
            Optional ByVal PVTcorr = Standing_based, _
            Optional ByVal ksep_fr = 0, _
            Optional ByVal p_ksep_atma = -1, _
            Optional ByVal t_ksep_C = -1, _
            Optional ByVal str_PVT As String = "" _
            ) ' VFM
' обязательные аргументы функции
' p_atma    давление, атм
' Т_C       температура, С.
'
' опциональные аргументы функции
' gamma_gas удельная плотность газа, по воздуху.
'           const_gg_ = 0.6
' gamma_oil удельная плотность нефти, по воде.
'           const_go_ = 0.86
' gamma_wat удельная плотность воды, по воде.
'           const_gw_ = 1
' rsb_m3m3  газосодержание при давлении насыщения, м3/м3.
'           const_rsb_default = 100
' rp_m3m3   замерной газовый фактор, м3/м3.
'           имеет приоритет перед rsb если Rp < rsb
' pb_atma   Давление насыщения при  температуре tres_C, атма.
'           Опциональный калибровочный параметр,
'           если не задан или = 0 то рассчитается по корреляции
' tres_C    пластовая температура, С.
'           Учитывается при расчете давления насыщения.
'           const_tres_default = 90
' bob_m3m3  объемный коэффициент нефти, м3/м3.
' muob_cP   вязкость нефти при давлении насыщения
'           По умолчанию рассчитывается по корреляции
' PVTcorr   номер набора PVT корреляций для расчета
'           Standing_based = 0 - на основе кор-ии Стендинга
'           McCain_based = 1 - на основе кор-ии Маккейна
'           straigth_line = 2 - на основе упрощенных зависимостей
' ksep_fr   коэффициент сепарации - определяет изменение свойств
'           нефти после сепарации доли свободного газа.
'           изменение свойств нефти зависит от условий
'           сепарации газа, которые должны быть явно заданы
' p_ksep_atma    давление при которой была сепарация
' t_ksep_C       температура при которой была сепарация
' str_PVT    закодированная строка с параметрами PVT.
'           если задана - перекрывает другие значения
'
' Возвращает значение объемного коэффициента газа, м3/м3
' для заданных термобарических условий.
' В основе расчета корреляция для z факотора
'description_end
        
On Error GoTo err1:
    Dim PVT As CPVT
    Dim i As Integer
    Dim ar1, ar2, arr
    Dim res As New Dictionary
    Const arr_len = 17
    ReDim ar1(16)
    ReDim ar2(16)
    ReDim arr(1, 16)
    
    Set PVT = readPVT(gamma_gas, gamma_oil, gamma_wat, rsb_m3m3, rp_m3m3, pb_atma, tres_C, bob_m3m3, _
                    muob_cP, PVTcorr, ksep_fr, p_ksep_atma, t_ksep_C, str_PVT)
    PVT.fw_perc = fw_perc
    PVT.qliq_sm3day = qliq_sm3day
    Call PVT.calc_PVT(p_atma, t_C)
    
    res.Add "rho_mix_rc_kgm3", PVT.rho_mix_rc_kgm3
    res.Add "rho_liq_rc_kgm3", PVT.rho_liq_rc_kgm3
    
    res.Add "mu_mix_cP", PVT.mu_mix_cP
    res.Add "mu_liq_cP", PVT.mu_liq_cP
    
    res.Add "cliq_JkgC", PVT.cliq_JkgC
    res.Add "cmix_JkgC", PVT.cmix_JkgC
    
    res.Add "sigma_liq_Nm", PVT.sigma_liq_Nm
           
    res.Add "q_mix_rc_m3day", PVT.q_mix_rc_m3day
    res.Add "q_oil_rc_m3day", PVT.q_oil_rc_m3day
    res.Add "q_wat_rc_m3day", PVT.q_wat_rc_m3day
    res.Add "q_gas_rc_m3day", PVT.q_gas_rc_m3day
    res.Add "q_gas_insitu_sm3day", PVT.q_gas_insitu_sm3day
    res.Add "q_gas_free_sm3day", PVT.q_gas_free_sm3day
    
    res.Add "mo_kgsec", PVT.mo_kgsec
    res.Add "mg_kgsec", PVT.mg_kgsec
    res.Add "mw_kgsec", PVT.mw_kgsec
    
    'res.Add "wg_kgsec", PVT.wg_kgsec
    'res.Add "wliq_kgsec", PVT.wliq_kgsec
    'res.Add "wm_kgsec", PVT.wm_kgsec
    'res.Add "wo_kgsec", PVT.wo_kgsec
    'res.Add "wwat_kgsec", PVT.wwat_kgsec
    
    res.Add "gas_fraction_d", PVT.gas_fraction_d
    
    For i = 1 To arr_len
        ar1(i - 1) = res.Items()(i - 1)
        ar2(i - 1) = res.Keys()(i - 1)
    Next i
    
    For i = LBound(ar1) To UBound(ar1)
            arr(0, i) = ar1(i)
            arr(1, i) = ar2(i)
        Next i
    
    MF_all_mf = arr
    Exit Function
err1:
    MF_all_mf = -1
    addLogMsg "Error:MF_all_mf:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' функция расчета коэффициента Джоуля Томсона
Public Function MF_CJT_Katm( _
             ByVal p_atma As Double, _
             ByVal t_C As Double, _
    Optional ByVal str_PVT As String = PVT_DEFAULT, _
    Optional ByVal qliq_sm3day As Double = 10, _
    Optional ByVal fw_perc As Double = 0)
' p_atma      - давление, атм
' t_C         - температура, С.
' str_PVT     - encoded to string PVT properties of fluid
' qliq_sm3day - liquid rate (at surface)
' fw_perc     - water fraction (watercut)
' output - number
'description_end
On Error GoTo err1:
    Dim PVT As New CPVT
    Set PVT = PVT_decode_string(str_PVT)
    PVT.qliq_sm3day = qliq_sm3day
    PVT.fw_perc = fw_perc
    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
    MF_CJT_Katm = PVT.cJT_Katm
    Exit Function
err1:
    MF_CJT_Katm = -1
    addLogMsg "Error:MF_CJT_Katm:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет объемного расхода газожидкостной смеси
' для заданных термобарических условий
Public Function MF_q_mix_rc_m3day( _
             ByVal qliq_sm3day As Double, _
             ByVal fw_perc As Double, _
             ByVal p_atma As Double, _
             ByVal t_C As Double, _
    Optional ByVal str_PVT As String = "")
' qliq_sm3day- дебит жидкости на поверхности
' fw_perc    - объемная обводненность
' p_atma     - давление, атм
' t_C        - температура, С.
' str_PVT    - закодированная строка с параметрами PVT.
'              если задана - перекрывает другие значения
' результат  - число - расход ГЖС, м3/сут.
'description_end
On Error GoTo err1:
    Dim PVT As CPVT
    Set PVT = PVT_decode_string(str_PVT)
    PVT.fw_perc = fw_perc
    PVT.qliq_sm3day = qliq_sm3day
    Call PVT.calc_PVT(p_atma, t_C)
    MF_q_mix_rc_m3day = PVT.q_mix_rc_m3day
    Exit Function
err1:
    MF_q_mix_rc_m3day = -1
    addLogMsg "Error:MF_q_mix_rc_m3day:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет плотности газожидкостной смеси для заданных  условий
Public Function MF_rho_mix_kgm3( _
             ByVal qliq_sm3day As Double, _
             ByVal fw_perc As Double, _
             ByVal p_atma As Double, _
             ByVal t_C As Double, _
    Optional ByVal str_PVT As String = "")
' qliq_sm3day- дебит жидкости на поверхности
' fw_perc    - объемная обводненность
' p_atma     - давление, атм
' t_C        - температура, С.
' str_PVT    - закодированная строка с параметрами PVT.
'              если задана - перекрывает другие значения
' результат  - число - плотность ГЖС, кг/м3.
'description_end
On Error GoTo err1:
    Dim PVT As CPVT
    Set PVT = PVT_decode_string(str_PVT)
    PVT.fw_perc = fw_perc
    PVT.qliq_sm3day = qliq_sm3day
    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
    MF_rho_mix_kgm3 = PVT.rho_mix_rc_kgm3
    Exit Function
err1:
    MF_rho_mix_kgm3 = -1
    addLogMsg "Error:MF_rho_mix_kgm3:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет вязкости газожидкостной смеси
' для заданных термобарических условий
Public Function MF_mu_mix_cP( _
            ByVal qliq_sm3day As Double, _
            ByVal fw_perc As Double, _
            ByVal p_atma As Double, _
            ByVal t_C As Double, _
   Optional ByVal str_PVT As String = "")
' qliq_sm3day - дебит жидкости на поверхности
' fw_perc     - объемная обводненность
' p_atma      - давление, атм
' t_C         - температура, С.
' str_PVT     - закодированная строка с параметрами PVT.
'              если задана - перекрывает другие значения
' результат   - число - вязкость ГЖС, м3/сут.
'description_end
On Error GoTo err1:
    Dim PVT As CPVT
    Set PVT = PVT_decode_string(str_PVT)
    PVT.fw_perc = fw_perc
    PVT.qliq_sm3day = qliq_sm3day
    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
    MF_mu_mix_cP = PVT.mu_mix_cP
    Exit Function
err1:
    MF_mu_mix_cP = -1
    addLogMsg "Error:MF_mu_mix_cP:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет доли газа в потоке
Public Function MF_gas_fraction_d( _
              ByVal p_atma As Double, _
              ByVal t_C As Double, _
     Optional ByVal fw_perc = 0, _
     Optional ByVal str_PVT As String = PVT_DEFAULT)
' p_atma   - давление, атм
' t_C      - температура, С.
' fw_perc  - обводненность объемная
' str_PVT  - закодированная строка с параметрами PVT.
'            если задана - перекрывает другие значения
' результат - число - доля газа в потоке
'              (расходная без проскальзования)
'description_end
On Error GoTo err1:
    Dim PVT As CPVT
    Set PVT = PVT_decode_string(str_PVT)
    PVT.fw_perc = fw_perc
    Call PVT.calc_PVT(CDbl(p_atma), CDbl(t_C))
    MF_gas_fraction_d = PVT.gas_fraction_d(0)
    Exit Function
err1:
    MF_gas_fraction_d = -1
    addLogMsg "Error:MF_gas_fraction_d:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет давления при котором
' достигается заданная доля газа в потоке
Public Function MF_p_gas_fraction_atma( _
               ByVal free_gas_d As Double, _
               ByVal t_C As Double, _
               ByVal fw_perc As Double, _
      Optional ByVal str_PVT As String = PVT_DEFAULT)
' free_gas_d - допустимая доля газа в потоке;
' t_C        - температура, С;
' fw_perc    - объемная обводненность, проценты %;
' str_PVT    - закодированная строка с параметрами PVT.
'              Если задана - перекрывает другие значения.
' результат  - число - давление, атма.
'description_end
On Error GoTo err1:
    Dim PVT As CPVT
    Set PVT = PVT_decode_string(str_PVT)
    PVT.fw_perc = fw_perc
    MF_p_gas_fraction_atma = PVT.p_gas_fraction_atma(free_gas_d, t_C)
    Exit Function
err1:
    MF_p_gas_fraction_atma = -1
    addLogMsg "Error:MF_p_gas_fraction_atma:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет газового фактора
' при котором достигается заданная доля газа в потоке
Public Function MF_rp_gas_fraction_m3m3( _
                ByVal free_gas_d As Double, _
                ByVal p_atma As Double, _
                ByVal t_C As Double, _
                ByVal fw_perc As Double, _
       Optional ByVal str_PVT As String = PVT_DEFAULT, _
       Optional ByVal Rp_limit_m3m3 As Double = 500)
' free_gas_d - допустимая доля газа в потоке
' p_atma     - давление, атм
' t_C        - температура, С.
' fw_perc    - объемная обводненность, проценты %;
' str_PVT    - закодированная строка с параметрами PVT.
'              если задана - перекрывает другие значения
' Rp_limit_m3m3 - верхняя граница оценки ГФ
' результат  - число - газовый фактор, м3/м3.
'description_end
On Error GoTo err1:
    Dim PVT As CPVT
    Set PVT = PVT_decode_string(str_PVT)
    PVT.fw_perc = fw_perc
    MF_rp_gas_fraction_m3m3 = PVT.rp_gas_fraction_m3m3(free_gas_d, p_atma, t_C, , Rp_limit_m3m3)
    Exit Function
err1:
    MF_rp_gas_fraction_m3m3 = -1
    addLogMsg "Error:MF_rp_gas_fraction_m3m3:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет натуральной сепарации газа на приеме насоса
Public Function MF_ksep_natural_d( _
             ByVal qliq_sm3day As Double, _
             ByVal fw_perc As Double, _
             ByVal p_intake_atma As Double, _
    Optional ByVal t_intake_C As Double = 50, _
    Optional ByVal d_intake_mm As Double = 90, _
    Optional ByVal d_cas_mm As Double = 120, _
    Optional ByVal str_PVT As String = PVT_DEFAULT)
' qliq_sm3day   - дебит жидкости в поверхностных условиях
' fw_perc       - обводненность
' p_intake_atma - давление сепарации
' t_intake_C    - температура сепарации
' d_intake_mm   - диаметр приемной сетки
' d_cas_mm      - диаметр эксплуатационной колонны
' str_PVT       - закодированная строка с параметрами PVT.
'                 если задана - перекрывает другие значения
' результат     - число - естественная сепарация
'description_end
On Error GoTo err1:
    Dim fluid As New CPVT
    Set fluid = PVT_decode_string(str_PVT)
    fluid.qliq_sm3day = qliq_sm3day
    fluid.fw_perc = fw_perc
    Call fluid.calc_PVT(p_intake_atma, t_intake_C)
    With fluid
        MF_ksep_natural_d = unf_calc_natural_separation(d_intake_mm / 1000, d_cas_mm / 1000, .qliq_sm3day, .q_gas_sm3day, .bo_m3m3, .bg_m3m3, .sigma_oil_gas_Nm, .rho_oil_sckgm3, .rho_gas_sckgm3, .fw_perc)
    End With
    Exit Function
err1:
    MF_ksep_natural_d = -1
    addLogMsg "Error:MF_ksep_natural_d:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет общей сепарации на приеме насоса
Public Function MF_ksep_total_d( _
        ByVal SepNat As Double, _
        ByVal SepGasSep As Double)
' SepNat        - естественная сепарация
' SepGasSep     - искусственная сепарация (газосепаратор)
    MF_ksep_total_d = SepNat + (1 - SepNat) * SepGasSep
End Function
'description_end
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'расчет градиента давления
'с использованием многофазных корреляций
Public Function MF_dpdl_atmm(ByVal d_m As Double, _
             ByVal p_atma As Double, _
             ByVal Ql_rc_m3day As Double, _
             ByVal Qg_rc_m3day As Double, _
    Optional ByVal mu_oil_cP As Double = const_mu_o, _
    Optional ByVal mu_gas_cP As Double = const_mu_g, _
    Optional ByVal sigma_oil_gas_Nm As Double = const_sigma_oil_Nm, _
    Optional ByVal gamma_oil As Double = const_go_, _
    Optional ByVal gamma_gas As Double = const_gg_, _
    Optional ByVal eps_m As Double = 0.0001, _
    Optional ByVal theta_deg As Double = 90, _
    Optional ByVal ZNLF As Boolean = False)
' расчет градиента давления по одной из корреляций
' объемные коэффициенты по умолчанию
' заданы равными единицам - если их не трогать,
' значит дебиты в рабочих условиях
' газосодержание равно нулю по умолчанию
'  - значит весь газ который указан идет в потоке
' пока только для Ансари - потом можно
' распространить и на другие методы
' d_m - диаметр трубы в которой идет поток
' p_atma - давление в точке расчета
' Ql_rc_m3day - дебит жидкости в рабочих условиях
' Qg_rc_m3day - дебит газа в рабочих условиях
' mu_oil_cP - вязкость нефти в рабочих условиях
' mu_gas_cP - вязкость газа в рабочих условиях
' sigma_oil_gas_Nm - поверхностное натяжение
'              жидкость газ
' gamma_oil - удельная плотность нефти
' gamma_gas - удельная плотность газа
' eps_m    - шероховатость
' theta_deg - угол от горизонтали
' ZNLF  - флаг для расчета барботажа
'description_end
    Dim rho_osc_kgm3 As Double
    Dim rho_gsc_kgm3 As Double
    Dim dPdLg_out_atmm As Double
    Dim dPdLf_out_atmm As Double
    Dim Vsl_out_msec As Double
    Dim Vsg_out_msec As Double
    Dim Hl_out_fr As Double
    Dim fpat_out_num
    Dim dPdLa_out_atmm As Double
    Dim PrGrad
    
On Error GoTo er1:
    rho_osc_kgm3 = gamma_oil * const_rho_ref
    rho_gsc_kgm3 = gamma_gas * const_rho_air
    PrGrad = unf_AnsariGradient(d_m, theta_deg, eps_m, _
                                Ql_rc_m3day, Qg_rc_m3day, _
                                mu_oil_cP, mu_gas_cP, _
                                sigma_oil_gas_Nm, _
                                rho_osc_kgm3, _
                                rho_gsc_kgm3, _
                                p_atma)
    MF_dpdl_atmm = PrGrad
    Exit Function
er1:
    MF_dpdl_atmm = -1
    addLogMsg "Error:MF_dpdl_atmm:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'  подбор параметров потока через трубу при известном
'  перепаде давления с использованием многофазных корреляций
Public Function MF_calibr_pipeline( _
                 ByVal qliq_sm3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal h_list_m As Variant, _
                 ByVal p_calc_from_atma As Double, _
                 ByVal p_calc_to_atma As Double, _
        Optional ByVal t_calc_from_C As Double = 50, _
        Optional ByVal calc_along_coord As Boolean = False, _
        Optional ByVal flow_along_coord As Boolean = False, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal diam_list_mm As Variant, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal t_amb_list_C As Variant, _
        Optional ByVal temp_method As TEMP_CALC_METHOD = StartEndTemp, _
        Optional ByVal c_calibr_grav = 1, _
        Optional ByVal c_calibr_fric = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal out_curves_num_points As Integer = 20, _
        Optional ByVal calibr_type As Integer = 0)
' qliq_sm3day - дебит жидкости в поверхностных условиях
'               если qliq_sm3day =0 и q_gas_sm3day > 0
'               тогда считается барботаж газа через жидкость
' fw_perc     - обводненность
' h_list_m    - траектория трубы.
'               число, range или таблица [0..N,0..1]
'p_calc_from_atma - давление начальное, атм
'                   граничное значение для проведения расчета
'p_calc_to_atma   - давление конечное, атм
'                   граничное значение для проведения расчета
' t_calc_from_C - температура в точке где задано давление расчета
' calc_along_coord - направление расчета относительно координаты
'                    если = 1 то расчет вдоль оси координат
'                    если = 0 то расчет против оси координат
' flow_along_coord - флаг направления потока относительно
'                    направления роста координаты
'                    если = 1 то поток вдоль оси координат
'                    если = 0 то поток против оси координат
' str_PVT     - закодированная строка с параметрами PVT.
'               если задана - перекрывает другие значения
'        если задан флаг gas_only = 1 то жидкость не учитывается
' diam_list_mm  - внутрнний диаметр трубы
'               число, range или таблица [0..N,0..1]
' hydr_corr   - гидравлическая корреляция, H_CORRELATION
'                  BeggsBrill = 0
'                  Ansari = 1
'                  Unified = 2
'                  Gray = 3
'                  HagedornBrown = 4
'                  SakharovMokhov = 5
' t_amb_list_C - температура окружающей среды, С
'                range или таблица [0..N,0..1]
' temp_method  - метод расчета температуры
'            0 - линейное распределение по длине
'            1 - температура равна температуре окружающей среды
'            2 - расчет температуры с учетом эмиссии в окр. среду
' c_calibr_grav - поправка на гравитационную составляющую
'               перепада давления
' c_calibr_fric - поправка на трение в перепаде давления
' roughness_m  - шероховатость трубы
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' out_curves_num_points - количество точек для вывода значений
'                   между концами трубы.
' calibr_type - тип калибровки
'             0 - подбор параметра c_calibr_grav
'             1 - подбор параметра c_calibr_fric
'             2 - подбор газового фактор
'             3 - подбор обводненности
' результат   - массив с подобранным парамером и подробностями.
'description_end
    Dim pipe As New CPipe
    Dim res(), res1()
    Dim coeffA(0 To 2)
    Dim func As String
    Dim cal_type_string As String
    Dim val_min As Double, val_max As Double
    Dim out, out_desc
    Dim prm As New CSolveParam
    
On Error GoTo err1:
    
    Set pipe = new_pipeline_with_stream(qliq_sm3day, _
                                        fw_perc, _
                                        h_list_m, _
                                        t_calc_from_C, _
                                        calc_along_coord, _
                                        flow_along_coord, _
                                        str_PVT, _
                                        diam_list_mm, _
                                        hydr_corr, _
                                        t_amb_list_C, _
                                        temp_method, _
                                        c_calibr_grav, _
                                        c_calibr_fric, _
                                        roughness_m, _
                                        q_gas_sm3day)
    
    ' prepare solution function
    Set coeffA(0) = pipe
        coeffA(1) = p_calc_from_atma
        coeffA(2) = p_calc_to_atma
        
    Select Case calibr_type
        Case 0
            func = "calc_pipe_dp_error_calibr_grav_atm"
            cal_type_string = "calibr_grav"
            val_min = 0.5
            val_max = 1.5
        Case 1
            func = "calc_pipe_dp_error_calibr_fric_atm"
            cal_type_string = "calibr_fric"
            val_min = 0.5
            val_max = 1.5
        Case 2
            func = "calc_pipe_dp_error_rp_atm"
            cal_type_string = "rp"
            val_min = pipe.fluid.rp_m3m3 * 0.5
            val_max = pipe.fluid.rp_m3m3 * 2
            ' Расширить диапазон поиска по газовому фактору может быть опасно
            ' так как возможна неоднозначность решения
            ' а текущий метод поиска работает только если есть одно решение
        Case 3
            func = "calc_pipe_dp_error_fw_atm"
            cal_type_string = "fw"
            val_min = 0
            val_max = 1
            If val_max > 1 Then val_max = 1
    End Select
    
    If solve_equation_bisection(func, val_min, val_max, coeffA, prm) Then
    
        out = Array(prm.x_solution, _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    Else
        out = Array("no solution", _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    End If
        
    out_desc = Array("solution", _
                     "cal_type", _
                     "y_solution", _
                     "iterations", _
                     "description")
    MF_calibr_pipeline = Array(out, out_desc)
    Exit Function
err1:
    MF_calibr_pipeline = Array(-1, "error")
    addLogMsg "Error:MF_calibr_pipeline:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'  подбор параметров потока через трубу при известном
'  перепаде давления с использованием многофазных корреляций
Public Function MF_calibr_pipe( _
        ByVal qliq_sm3day As Double, _
        ByVal fw_perc As Double, _
        ByVal length_m As Double, _
        ByVal p_calc_from_atma As Double, _
        ByVal p_calc_to_atma As Double, _
        ByVal calc_along_flow As Boolean, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal theta_deg As Double = 90, _
        Optional ByVal d_mm As Double = 60, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal t_calc_from_C As Double = 50, _
        Optional ByVal t_calc_to_C As Double = -1, _
        Optional ByVal c_calibr_grav = 1, _
        Optional ByVal c_calibr_fric = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal calibr_type As Integer = 0)
' qliq_sm3day     - дебит жидкости в поверхностных условиях
'              если qliq_sm3day =0 и q_gas_sm3day > 0
'              тогда считается барботаж газа через жидкость
' fw_perc         - обводненность
' length_m        - Длина трубы, измеренная, м
'p_calc_from_atma - давление начальное, атм
'                   граничное значение для проведения расчета
'p_calc_to_atma   - давление конечное, атм
'                   граничное значение для проведения расчета
' calc_along_flow - флаг направления расчета относительно потока
'                   если = 1 то расчет по потоку
'                   если = 0 то расчет против потока
' str_PVT         - закодированная строка с параметрами PVT.
'                   если задана - перекрывает другие значения
'        если задан флаг gas_only = 1 то жидкость не учитывается
' theta_deg       - угол направления потока к горизонтали
'                   ( 90 - вертикальная труба поток вверх
'                    -90 - вертикальная труба поток вниз)
'                   может принимать отрицательные значения
' d_mm            - внутрнний диаметр трубы
' hydr_corr       - гидравлическая корреляция, H_CORRELATION
'                    BeggsBrill = 0
'                    Ansari = 1
'                    Unified = 2
'                    Gray = 3
'                    HagedornBrown = 4
'                    SakharovMokhov = 5
' t_calc_from_C   - температура в точке где задано давление, С
' t_calc_to_C     - температура на другом конце трубы
'                 по умолчанию температура вдоль трубы постоянна
'                   если задано то меняется линейно по трубе
' c_calibr_grav   - поправка на гравитационную составляющую
'                   перепада давления
' c_calibr_fric   - поправка на трение в перепаде давления
' roughness_m     - шероховатость трубы
' q_gas_sm3day    - свободный газ. дополнительный к PVT потоку.
' calibr_type - тип калибровки
'             0 - подбор параметра c_calibr_grav
'             1 - подбор параметра c_calibr_fric
'             2 - подбор газового фактор
'             3 - подбор обводненности
' результат   - массив с подобранным парамером и подробностями.
'description_end
    
    
    Dim pipe As New CPipe
    Dim prm As New CSolveParam
    Dim coeffA(0 To 2)
    Dim func As String
    Dim cal_type_string As String
    Dim val_min As Double, val_max As Double
    Dim out, out_desc
On Error GoTo err1:
    ' check pipe length
    length_m = Abs(length_m)                ' length must be positive
    If length_m = 0 Then
        MF_calibr_pipe = Array(p_calc_from_atma, t_calc_from_C)
        Exit Function
    End If
    ' initialize pipe
    Set pipe = new_pipe_with_stream(qliq_sm3day, fw_perc, length_m, calc_along_flow, _
                                    str_PVT, theta_deg, d_mm, hydr_corr, _
                                    t_calc_from_C, t_calc_to_C, _
                                    c_calibr_grav, c_calibr_fric, _
                                    roughness_m, q_gas_sm3day)
    ' prepare solution function
    Set coeffA(0) = pipe
        coeffA(1) = p_calc_from_atma
        coeffA(2) = p_calc_to_atma
        
    Select Case calibr_type
        Case 0
            func = "calc_pipe_dp_error_calibr_grav_atm"
            cal_type_string = "calibr_grav"
            val_min = 0.5
            val_max = 1.5
        Case 1
            func = "calc_pipe_dp_error_calibr_fric_atm"
            cal_type_string = "calibr_fric"
            val_min = 0.5
            val_max = 1.5
        Case 2
            func = "calc_pipe_dp_error_rp_atm"
            cal_type_string = "rp"
            val_min = pipe.fluid.rp_m3m3 * 0.5
            val_max = pipe.fluid.rp_m3m3 * 2
            ' Расширить диапазон поиска по газовому фактору может быть опасно
            ' так как возможна неоднозначность решения
            ' а текущий метод поиска работает только если есть одно решение
        Case 3
            func = "calc_pipe_dp_error_fw_atm"
            cal_type_string = "fw"
            val_min = 0
            val_max = 1
            If val_max > 1 Then val_max = 1
    End Select
    
    If solve_equation_bisection(func, val_min, val_max, coeffA, prm) Then
    
        out = Array(prm.x_solution, _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    Else
        out = Array("no solution", _
                    cal_type_string, _
                    prm.y_solution, _
                    prm.iterations, _
                    prm.msg)
    End If
        
        out_desc = Array("solution", _
                         "cal_type", _
                         "y_solution", _
                         "iterations", _
                         "description")
        MF_calibr_pipe = Array(out, out_desc)
    
    Exit Function
err1:
    MF_calibr_pipe = Array(-1, "error")
    addLogMsg "Error:MF_calibr_pipe:" & Err.Description
    
End Function
''description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
''  подбор параметров потока через трубу при известном
''  перепаде давления с использованием многофазных корреляций
'Public Function MF_fit_pipe_m3day( _
'        ByVal qliq_sm3day As Double, _
'        ByVal fw_perc As Double, _
'        ByVal length_m As Double, _
'        ByVal p_calc_atma As Double, _
'        ByVal calc_along_flow As Boolean, _
'        Optional ByVal str_PVT As String = PVT_DEFAULT, _
'        Optional ByVal theta_deg As Double = 90, _
'        Optional ByVal d_mm As Double = 60, _
'        Optional ByVal hydr_corr As H_CORRELATION = 0, _
'        Optional ByVal t_in_C As Double = 50, _
'        Optional ByVal t_out_C As Double = -1, _
'        Optional ByVal c_calibr_grav = 1, _
'        Optional ByVal c_calibr_fric = 1, _
'        Optional ByVal roughness_m As Double = 0.0001, _
'        Optional ByVal calibr_type As Integer = 0)
'' Обязательные параметры
'' qliq_sm3day - дебит жидкости в поверхностных условиях
'' fw_perc     - обводненность
'' Length_m    - Длина трубы, измеренная, м
'' calc_along_flow - флаг направления расчета относительно потока
''     если = 1 то расчет по потоку
''     если = 0 то расчет против потока
'' p_calc_atma  - давление с которого начинается расчет, атм
''               граничное значение для проведения расчета
'' str_PVT      - закодированная строка с параметрами PVT.
''               если задана - перекрывает другие значения
'' theta_deg   - угол направления потока к горизонтали
''               (90 - вертикальная труба поток вверх
''                -90 - вертикальная труба поток вниз)
''               может принимать отрицательные значения
'' d_mm        - внутрнний диаметр трубы
'' hydr_corr    - гидравлическая корреляция, H_CORRELATION
''                  BeggsBrill = 0
''                  Ansari = 1
''                  Unified = 2
''                  Gray = 3
''                  HagedornBrown = 4
''                  SakharovMokhov = 5
'' t_in_C       - температура на входе потока в трубу, С
'' t_out_C      - температура на выходе потока из трубы, С
''               по умолчанию температура вдоль трубы постоянна
''               если задано то меняется линейно по трубе
'' c_calibr_grav  - поправка на гравитационную составляющую
''               перепада давления
'' c_calibr_fric  - поправка на трение в перепаде давления
'' roughness_m - шероховатость трубы
'' calibr_type - тип калибровки
''             0 - подбор параметра c_calibr_grav
''             1 - подбор параметра c_calibr_fric
''             2 - подбор газового фактор
''             3 - подбор обводненности
'' результат   - число - давление на другом конце трубы atma.
''description_end
'
'
'    Dim pipe As New CPipe
'    Dim PTin As PTtype, PTout As PTtype
'    Dim TM As TEMP_CALC_METHOD
'    Dim prm As New CSolveParam
'    Dim coeffA(0 To 2)
'
'On Error GoTo err1:
'
'
'    Exit Function
'err1:
'    MF_fit_pipe_m3day = Array(-1, "error")
'    addLogMsg "Error:MF_fit_pipe_m3day:" & Err.Description
'End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
'  расчет распределения давления и температуры в трубопроводе
'  с использованием многофазных корреляций
Public Function MF_p_pipeline_atma( _
                 ByVal qliq_sm3day As Double, _
                 ByVal fw_perc As Double, _
                 ByVal h_list_m As Variant, _
                 ByVal p_calc_from_atma As Double, _
        Optional ByVal t_calc_from_C As Double = 50, _
        Optional ByVal calc_along_coord As Boolean = False, _
        Optional ByVal flow_along_coord As Boolean = False, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal diam_list_mm As Variant, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal t_amb_list_C As Variant, _
        Optional ByVal temp_method As TEMP_CALC_METHOD = StartEndTemp, _
        Optional ByVal c_calibr_grav = 1, _
        Optional ByVal c_calibr_fric = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal out_curves_num_points As Integer = 20)
' qliq_sm3day - дебит жидкости в поверхностных условиях
'               если qliq_sm3day =0 и q_gas_sm3day > 0
'               тогда считается барботаж газа через жидкость
' fw_perc     - обводненность
' h_list_m    - траектория трубы.
'               число, range или таблица [0..N,0..1]
' p_calc_from_atma  - давление с которого начинается расчет, атм
'               граничное значение для проведения расчета
' t_calc_from_C - температура в точке где задано давление расчета
' calc_along_coord - направление расчета относительно координаты
'                    если = 1 то расчет вдоль оси координат
'                    если = 0 то расчет против оси координат
' flow_along_coord - флаг направления потока относительно
'                    направления роста координаты
'                    если = 1 то поток вдоль оси координат
'                    если = 0 то поток против оси координат
' str_PVT     - закодированная строка с параметрами PVT.
'               если задана - перекрывает другие значения
'        если задан флаг gas_only = 1 то жидкость не учитывается
' diam_list_mm  - внутрнний диаметр трубы
'               число, range или таблица [0..N,0..1]
' hydr_corr   - гидравлическая корреляция, H_CORRELATION
'                  BeggsBrill = 0
'                  Ansari = 1
'                  Unified = 2
'                  Gray = 3
'                  HagedornBrown = 4
'                  SakharovMokhov = 5
' t_amb_list_C - температура окружающей среды, С
'                range или таблица [0..N,0..1]
' temp_method  - метод расчета температуры
'            0 - линейное распределение по длине
'            1 - температура равна температуре окружающей среды
'            2 - расчет температуры с учетом эмиссии в окр. среду
' c_calibr_grav - поправка на гравитационную составляющую
'               перепада давления
' c_calibr_fric - поправка на трение в перепаде давления
' roughness_m   - шероховатость трубы
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' out_curves_num_points - количество точек для вывода значений
'                   между концами трубы.
' результат    - число - давление на другом конце трубы atma.
'description_end
    Dim pipe As New CPipe
    Dim res, res1
On Error GoTo err1:
    
    Set pipe = new_pipeline_with_stream(qliq_sm3day, _
                                        fw_perc, _
                                        h_list_m, _
                                        t_calc_from_C, _
                                        calc_along_coord, _
                                        flow_along_coord, _
                                        str_PVT, _
                                        diam_list_mm, _
                                        hydr_corr, _
                                        t_amb_list_C, _
                                        temp_method, _
                                        c_calibr_grav, _
                                        c_calibr_fric, _
                                        roughness_m, _
                                        q_gas_sm3day)
    
    ' calc pressure distribution
    res1 = PT_to_array(pipe.calc_dPipe(p_calc_from_atma, t_calc_from_C, allCurves))
    
    res = pipe.array_out(out_curves_num_points)
    res(0, 0) = res1(0)
    MF_p_pipeline_atma = res
    Exit Function
err1:
    MF_p_pipeline_atma = Array(-1, "error")
    addLogMsg "Error:MF_p_pipeline_atma:" & Err.Description
End Function
Public Function MF_test(ByVal value As Variant)
    MF_test = value
End Function
'  расчет распределения давления и температуры в трубе
'  с использованием многофазных корреляций
Public Function MF_p_pipe_atma( _
        ByVal qliq_sm3day As Double, _
        ByVal fw_perc As Double, _
        ByVal length_m As Double, _
        ByVal p_calc_from_atma As Double, _
        ByVal calc_along_flow As Boolean, _
        Optional ByVal str_PVT As String = PVT_DEFAULT, _
        Optional ByVal theta_deg As Double = 90, _
        Optional ByVal d_mm As Double = 60, _
        Optional ByVal hydr_corr As H_CORRELATION = 0, _
        Optional ByVal t_calc_from_C As Double = 50, _
        Optional ByVal t_calc_to_C As Double = -1, _
        Optional ByVal c_calibr_grav = 1, _
        Optional ByVal c_calibr_fric = 1, _
        Optional ByVal roughness_m As Double = 0.0001, _
        Optional ByVal q_gas_sm3day As Double = 0, _
        Optional ByVal out_curves As Boolean = True, _
        Optional ByVal out_curves_num_points As Integer = 20)
' qliq_sm3day     - дебит жидкости в поверхностных условиях
'              если qliq_sm3day =0 и q_gas_sm3day > 0
'              тогда считается барботаж газа через жидкость
' fw_perc         - обводненность
' length_m        - Длина трубы, измеренная, м
'p_calc_from_atma - давление с которого начинается расчет, атм
'                   граничное значение для проведения расчета
' calc_along_flow - флаг направления расчета относительно потока
'                   если = 1 то расчет по потоку
'                   если = 0 то расчет против потока
' str_PVT         - закодированная строка с параметрами PVT.
'                   если задана - перекрывает другие значения
'        если задан флаг gas_only = 1 то жидкость не учитывается
' theta_deg       - угол направления потока к горизонтали
'                   ( 90 - вертикальная труба поток вверх
'                    -90 - вертикальная труба поток вниз)
'                   может принимать отрицательные значения
' d_mm            - внутрнний диаметр трубы
' hydr_corr       - гидравлическая корреляция, H_CORRELATION
'                    BeggsBrill = 0
'                    Ansari = 1
'                    Unified = 2
'                    Gray = 3
'                    HagedornBrown = 4
'                    SakharovMokhov = 5
' t_calc_from_C   - температура в точке где задано давление, С
' t_calc_to_C     - температура на другом конце трубы
'                 по умолчанию температура вдоль трубы постоянна
'                   если задано то меняется линейно по трубе
' c_calibr_grav   - поправка на гравитационную составляющую
'                   перепада давления
' c_calibr_fric   - поправка на трение в перепаде давления
' roughness_m     - шероховатость трубы
' q_gas_sm3day    - свободный газ. дополнительный к PVT потоку.
' out_curves      - флаг определяет вывод расчетных значений
'                   между концами трубы.
'                   вывод может замедлять расчет (не сильно)
' out_curves_num_points - количество точек для вывода значений
'                   между концами трубы.
' результат       - число - давление на другом конце трубы atma.
'                  или массив - первая строка значения
'                               вторая строка - подписи
'description_end
    Dim pipe As CPipe
    Dim PVT As New CPVT
    Dim PTcalc As PTtype
    Dim PTin As PTtype
    Dim PTout As PTtype
    Dim TM As TEMP_CALC_METHOD
    Dim out, out_desc
    Dim out_curves_type As CALC_RESULTS
    Dim res
On Error GoTo err1:
    
    ' check pipe length
    length_m = Abs(length_m)                ' length must be positive
    If length_m = 0 Then
        MF_p_pipe_atma = Array(p_calc_from_atma, t_calc_from_C)
        Exit Function
    End If
    ' initialize pipe
    Set pipe = new_pipe_with_stream(qliq_sm3day, fw_perc, length_m, calc_along_flow, _
                                    str_PVT, theta_deg, d_mm, hydr_corr, _
                                    t_calc_from_C, t_calc_to_C, _
                                    c_calibr_grav, c_calibr_fric, _
                                    roughness_m, q_gas_sm3day)
    ' prep output
    If out_curves Then
        out_curves_type = allCurves
    Else
        out_curves_type = nocurves
    End If
    ' calc pressure distribution
    PTcalc = pipe.calc_dPipe(p_calc_from_atma, , out_curves_type)  ' ( -allcurves * out_curves ) can be used for simpcity but not
    
    ' prep results for output
    If calc_along_flow Then
        PTout = PTcalc
        PTin.p_atma = p_calc_from_atma
        PTin.t_C = t_calc_from_C
    Else
        PTin = PTcalc
        PTout.p_atma = p_calc_from_atma
        PTout.t_C = t_calc_from_C
    End If
    ' out rusults based on out_curves value
    If out_curves Then
        res = pipe.array_out(out_curves_num_points)
        res(0, 0) = PTcalc.p_atma
        res(0, 1) = PTcalc.t_C
        MF_p_pipe_atma = res
        
    Else
        out = Array(PTcalc.p_atma, _
                    PTcalc.t_C, _
                    PTin.p_atma, _
                    PTin.t_C, _
                    PTout.p_atma, _
                    PTout.t_C, _
                    pipe.c_calibr_grav, _
                    pipe.c_calibr_fric)
        
        out_desc = Array("p_calc_atma", _
                         "t_calc_C", _
                         "p_in_atma", _
                         "t_in_C", _
                         "p_out_atma", _
                         "t_out_C", _
                         "c_calibr_grav", _
                         "c_calibr_fric")
        
        MF_p_pipe_atma = Array(out, out_desc)
    
    End If
    
    Exit Function
err1:
    MF_p_pipe_atma = Array(-1, "error")
    addLogMsg "Error:MF_p_pipe_atma:" & Err.Description
End Function
' ==============  функции для расчета штуцера ==========================
' =====================================================================
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет давления в штуцере
Public Function MF_p_choke_atma( _
            ByVal qliq_sm3day As Double, _
            ByVal fw_perc As Double, _
            ByVal d_choke_mm As Double, _
            Optional ByVal p_calc_from_atma As Double = -1, _
            Optional ByVal calc_along_flow As Boolean = True, _
            Optional ByVal d_pipe_mm As Double = 70, _
            Optional ByVal t_choke_C As Double = 20, _
            Optional ByVal c_calibr_fr As Double = 1, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal q_gas_sm3day As Double = 0)
' qliq_sm3day   - дебит жидкости в поверхностных условиях
' fw_perc       - обводненность
' d_choke_mm     - диаметр штуцера (эффективный)
' p_calc_from_atma    - давление с которого начинается расчет, атм
'                 граничное значение для проведения расчета
'                 либо давление на входе, либое на выходе
' calc_along_flow - флаг направления расчета относительно потока
'     если = 1 то расчет по потоку
'     ищется давление на выкиде по известному давлению на входе,
'     ищется линейное давление по известному буферному
'     если = 0 то расчет против потока
'     ищется давление на входе по известному давлению на выходе,
'     ищется буферное давление по известному линейному
' d_pipe_mm      - диаметр трубы до и после штуцера
' t_choke_C      - температура, С.
' c_calibr_fr   - поправочный коэффициент на штуцер
'                 1 - отсутсвие поправки
'                 Q_choke_real = c_calibr_fr * Q_choke_model
' str_PVT        - закодированная строка с параметрами PVT.
'                 если задана - перекрывает другие значения
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' результат     - число - давления на штуцере на расчетной стороне.
'                массив значений с параметрами штуцера
'description_end
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim pt As PTtype
    Dim pres As Double
    Dim PVT As CPVT
    Dim out, out_desc
    Dim p_in_atma As Double
    Dim p_out_atma As Double
    
    
    Set PVT = PVT_decode_string(str_PVT)
    PVT.q_gas_free_sm3day = q_gas_sm3day
    Set choke.fluid = PVT
    choke.fluid.qliq_sm3day = qliq_sm3day
    choke.fluid.fw_perc = fw_perc
    choke.d_down_m = d_pipe_mm / 1000
    choke.d_up_m = d_pipe_mm / 1000
    choke.d_choke_m = d_choke_mm / 1000
    choke.c_calibr_fr = c_calibr_fr
    If PVT.gas_only Then
        pres = GLV_p_atma(d_choke_mm, p_calc_from_atma, q_gas_sm3day, PVT.gamma_g, t_choke_C, calc_along_flow)(0)(0)
        out = pres
        If calc_along_flow Then
            p_in_atma = p_calc_from_atma
            p_out_atma = out
            out_desc = "Pout, atma"
        Else
            p_out_atma = p_calc_from_atma
            p_in_atma = out
            out_desc = "Pin, atma"
        End If
    Else
        If calc_along_flow Then
            p_in_atma = p_calc_from_atma
            pt = choke.calc_choke_p_lin(set_PT(p_in_atma, t_choke_C))
            out = pt.p_atma
            p_out_atma = out
            out_desc = "Pout, atma"
        Else
            p_out_atma = p_calc_from_atma
            pt = choke.calc_choke_p_buf(set_PT(p_out_atma, t_choke_C))
            out = pt.p_atma
            p_in_atma = out
            out_desc = "Pin, atma"
        End If
    End If
    MF_p_choke_atma = Array(Array(out, p_in_atma, p_out_atma, t_choke_C, choke.c_calibr_fr, PVT.q_gas_sm3day), _
                           Array(out_desc, "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr", "q_gas_sm3day"))  ' на выходе выдаем массив
    Exit Function
err1:
    MF_p_choke_atma = -1
    addLogMsg "Error:MF_p_choke_atma:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
' расчет корректирующего фактора (множителя) модели штуцера под замеры
Public Function MF_calibr_choke_fr( _
            ByVal qliq_sm3day As Double, _
            ByVal fw_perc As Double, _
            ByVal d_choke_mm As Double, _
            Optional ByVal p_in_atma As Double = -1, _
            Optional ByVal p_out_atma As Double = -1, _
            Optional ByVal d_pipe_mm As Double = 70, _
            Optional ByVal t_choke_C As Double = 20, _
            Optional ByVal str_PVT As String = PVT_DEFAULT, _
            Optional ByVal q_gas_sm3day As Double = 0)
' qliq_sm3day   - дебит жидкости в ст. условиях
' fw_perc       - обводненность
' d_choke_mm    - диаметр штуцера (эффективный), мм
' p_in_atma     - давление на входе (высокой стороне)
' p_out_atma    - давление на выходе (низкой стороне)
' d_pipe_mm     - диаметр трубы до и после штуцера, мм
' t_choke_C     - температура, С.
' str_PVT       - закодированная строка с параметрами PVT,
'                 если задана - перекрывает другие значения
' q_gas_sm3day  - свободный газ. дополнительный к PVT потоку.
' результат     - число - калибровочный коэффициент для модели.
'                 штуцера  - множитель на дебит через штуцер
'description_end
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim pt As PTtype
    Dim PVT As CPVT
    Dim out, out_desc
    
    Set PVT = PVT_decode_string(str_PVT)
    PVT.q_gas_free_sm3day = q_gas_sm3day
    Set choke.fluid = PVT
    choke.fluid.qliq_sm3day = qliq_sm3day
    choke.fluid.fw_perc = fw_perc
    choke.d_down_m = d_pipe_mm / 1000
    choke.d_up_m = d_pipe_mm / 1000
    choke.d_choke_m = d_choke_mm / 1000
    
    If p_in_atma > p_out_atma And p_out_atma >= 1 Then
        Call choke.calc_choke_calibration(p_in_atma, p_out_atma, t_choke_C)
        out = choke.c_calibr_fr
        out_desc = "c_calibr_fr"
    End If
    MF_calibr_choke_fr = Array(Array(out, p_in_atma, p_out_atma, t_choke_C, choke.c_calibr_fr), _
                         Array(out_desc, "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr"))  ' на выходе выдаем массив
    Exit Function
err1:
    MF_calibr_choke_fr = -1
    addLogMsg "Error:MF_calibr_choke_fr:" & Err.Description
End Function
'description_to_manual      - для автогенерации описания - помещает заголовок функции и окружающие комментарии в мануал (со след строки)
 ' функция расчета дебита жидкости через штуцер
 ' при заданном входном и выходном давлениях
Public Function MF_q_choke_sm3day( _
        ByVal fw_perc As Double, _
        ByVal d_choke_mm As Double, _
        ByVal p_in_atma As Double, _
        ByVal p_out_atma As Double, _
        Optional ByVal d_pipe_mm As Double = 70, _
        Optional ByVal t_choke_C = 20, _
        Optional ByVal c_calibr_fr As Double = 1, _
        Optional ByVal str_PVT As String = PVT_DEFAULT)
' fw_perc      - обводненность
' d_choke_mm    - диаметр штуцера (эффективный)
' p_in_atma    - давление на входе (высокой стороне)
' p_out_atma   - давление на выходе (низкой стороне)
' d_pipe_mm    - диаметр трубы до и после штуцера
' t_choke_C    - температура, С.
' c_calibr_fr  - поправочный коэффициент на штуцер
'                 1 - отсутсвие поправки (по умолчанию)
'                 Q_choke_real = c_calibr_fr * Q_choke_model
' str_PVT      - закодированная строка с параметрами PVT.
'                 если задана - перекрывает другие значения
' результат    - расход и массив результатов
'description_end
On Error GoTo err1:
    Dim choke As New Cchoke
    Dim PVT As CPVT
    Dim Q As Double
    
    Set PVT = PVT_decode_string(str_PVT)
    Set choke.fluid = PVT
    choke.d_down_m = d_pipe_mm / 1000
    choke.d_up_m = d_pipe_mm / 1000
    choke.d_choke_m = d_choke_mm / 1000
    choke.fluid.fw_perc = fw_perc
    choke.c_calibr_fr = c_calibr_fr
    If PVT.gas_only Then
        Q = GLV_q_gas_sm3day(d_choke_mm, p_in_atma, p_out_atma, PVT.gamma_g, t_choke_C, c_calibr_fr)(0)(0)
        MF_q_choke_sm3day = Array(Array(Q, p_in_atma, p_out_atma, t_choke_C, c_calibr_fr), _
                            Array("Qgas", "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr"))
    Else
        Q = choke.calc_choke_qliq_sm3day(p_in_atma, p_out_atma, t_choke_C)
        MF_q_choke_sm3day = Array(Array(Q, p_in_atma, p_out_atma, t_choke_C, c_calibr_fr), _
                            Array("Qliq", "p_intake_atma", "p_out_atma", "t_choke_C", "c_calibr_fr"))
    End If
    
    Exit Function
err1:
    MF_q_choke_sm3day = -1
    addLogMsg "Error:MF_q_choke_sm3day:" & Err.Description
End Function
' вспомогательная функция для создания PVT объекта из исходных данных
Private Function readPVT(Optional ByVal gamma_gas As Double = const_gg_, _
                    Optional ByVal gamma_oil As Double = const_go_, _
                    Optional ByVal gamma_wat As Double = const_gw_, _
                    Optional ByVal rsb_m3m3 = const_rsb_default, _
                    Optional ByVal rp_m3m3 = -1, _
                    Optional ByVal pb_atma = -1, _
                    Optional ByVal tres_C = const_tres_default, _
                    Optional ByVal bob_m3m3 = -1, _
                    Optional ByVal muob_cP = -1, _
                    Optional ByVal PVTcorr = Standing_based, _
                    Optional ByVal ksep_fr = 0, _
                    Optional ByVal p_ksep_atma = -1, _
                    Optional ByVal t_ksep_C = -1, _
                    Optional ByVal str_PVT As String = "" _
                    ) As CPVT 'VFM
    Dim PVT As CPVT
    If str_PVT <> "" Then
        Set PVT = PVT_decode_string(str_PVT)
    Else
        Set PVT = New CPVT
        PVT.Init gamma_gas, gamma_oil, gamma_wat, rsb_m3m3, pb_atma, bob_m3m3, PVTcorr, tres_C, rp_m3m3, muob_cP
        If ksep_fr > 0 And ksep_fr <= 1 And p_ksep_atma > 0 And t_ksep_C > 0 Then
            Call PVT.mod_after_separation(p_ksep_atma, t_ksep_C, ksep_fr, GasGoesIntoSolution)
        End If
    End If
    Set readPVT = PVT
End Function

